---
title: pong source
nav: true
---

```
#include <Arduino.h>
#include <Arduboy2.h>

Arduboy2 arduboy;

// ball initial position center of the screen
int16_t ballx = WIDTH / 2;
int16_t bally = HEIGHT / 2;
// ball velocity in x and y direction
int8_t vx = 1;
int8_t vy = 1;
// ball radius
const int8_t ballr = 2;


// player1 paddle
int16_t p1width = 3;
int16_t p1height = 10;
int16_t p1y = HEIGHT / 2 - p1height / 2;
const int16_t p1x = 0;

// player2 paddle
int16_t p2width = 3;
int16_t p2height = 10;
int16_t p2y = HEIGHT / 2 - p2height / 2;
const int16_t p2x = WIDTH - p2width;

uint16_t p1score = 0;
uint16_t p2score = 0;
const uint16_t maxpoints = 5;


const int GAME_TITLE = 0;
const int GAME_PLAY = 1;
const int GAME_WON = 2;
uint8_t gamestate = GAME_TITLE;



// setup will be ran first
void setup()
{
  arduboy.begin();
  arduboy.initRandomSeed();
  arduboy.setFrameRate(60);
  arduboy.clear();
}

void resetBall()
{
  // move the ball to the center of the screen
  ballx = WIDTH / 2;
  bally = HEIGHT / 2;

  // randomize the ball starting direction
  if (random(0, 2))
  {
    vx = -vx;
  }
  if (random(0, 2))
  {
    vy = -vy;
  }
}

void handleBall()
{
  // move the ball
  ballx += vx;
  bally += vy;

  // keep the ball inside our playfield
  if (
      bally - ballr <= 0         // bounce from the top
      || bally + ballr >= HEIGHT // and from the bottom
  )
  {
    // reverse up down
    vy = -vy;
  }

  // check if we hit either paddle
  // player1 paddle
  if (ballx - ballr == p1x + p1width    // ball is at the paddle surface
      && bally + ballr > p1y            // and between the paddle top position
      && bally - ballr < p1y + p1height // and between the paddle bottom position
  )
  {
    vx = -vx;
  }
  // and player2 paddle
  if (ballx + ballr == p2x              // ball is at the paddle surface
      && bally + ballr > p2y            // and between the paddle position
      && bally - ballr < p2y + p2height // and between the paddle position
  )
  {
    vx = -vx;
  }

  // if ball is over the left or right side resetball
  // over right side
  if (ballx + ballr < 0)
  {
    p2score++;
    resetBall();
  }
  // over left side
  if (ballx - ballr > WIDTH)
  {
    p1score++;
    resetBall();
  }

}

void draw()
{
  // draw center line
  uint16_t spacing = 6;
  for (int i = 0; i < HEIGHT; i += spacing)
  {
    arduboy.drawLine(
        WIDTH / 2, i,              // first loop 64, 0 second 64, 6
        WIDTH / 2, i + spacing / 2 // first loop 64, 3 second 64, 9
    );
  }

  arduboy.setTextSize(2);
  // player1 score on the left side
  arduboy.setCursor(20, 5);
  arduboy.print(p1score);

  // player2 score on the right side
  arduboy.setCursor(WIDTH - 20 - 12, 5);
  arduboy.print(p2score);



  // draw the ball
  arduboy.fillCircle(ballx, bally, ballr);


  // draw both paddles
  arduboy.fillRect(p1x, p1y, p1width, p1height);
  arduboy.fillRect(p2x, p2y, p2width, p2height);
}


void handlePaddles()
{
  // player1 paddle
  if (arduboy.pressed(UP_BUTTON))
  {
    if (p1y > 0)
    {
      p1y--;
    }
  }
  if (arduboy.pressed(DOWN_BUTTON))
  {
    if (p1y + p1height < HEIGHT)
    {
      p1y++;
    }
  }
  // player 2 paddle
  if (arduboy.pressed(B_BUTTON))
  {
    if (p2y > 0)
    {
      p2y--;
    }
  }
  // player 2 paddle
  if (arduboy.pressed(A_BUTTON))
  {
    if (p2y + p2height < HEIGHT)
    {
      p2y++;
    }
  }
}

void titleScreen()
{
  if (
      arduboy.pressed(UP_BUTTON)
      || arduboy.pressed(DOWN_BUTTON)
      || arduboy.pressed(A_BUTTON)
      || arduboy.pressed(B_BUTTON))
  {
    resetBall();
    gamestate = GAME_PLAY;
  }
}


void winScreen()
{
  // textsize 2 is 12x16 px
  arduboy.setTextSize(2);

  // print who won about center of the screen
  arduboy.setCursor(34, 23);
  if (p1score > p2score)
  {
    arduboy.print("P1");
  }
  else
  {
    arduboy.print("P2");
  }
  arduboy.print(" won");

  // display right away
  arduboy.display();
  // and sleep for a moment so that there is time to react and not start a new game immediately
  delay(500);

  if (
      arduboy.pressed(UP_BUTTON) || arduboy.pressed(DOWN_BUTTON) || arduboy.pressed(A_BUTTON) ||
      arduboy.pressed(B_BUTTON))
  {
    // start a new game
    resetBall();
    p1score = 0;
    p2score = 0;
    gamestate = GAME_PLAY;
  }
}



// loop runs all the time
void loop()
{
  if (!arduboy.nextFrame())
  {
    return;
  }
  arduboy.clear();


  draw();


  // dependig on the state we will do different things
  switch (gamestate)
  {
  // title screen, waits for input and moves to GAME_PLAY
  case GAME_TITLE:
    titleScreen();
    break;
  // game is playing untill either side wins, moves to GAME_WON
  case GAME_PLAY:
    handlePaddles();
    handleBall();
    if (p1score >= maxpoints || p2score >= maxpoints)
    {
      // go to win state
      gamestate = GAME_WON;
    }
    break;
  // game won by a player
  case GAME_WON:
    winScreen();
    break;
  }


  arduboy.display();
}
```